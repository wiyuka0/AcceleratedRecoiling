buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // 引入 ASM 依赖，用于处理字节码
        classpath 'org.ow2.asm:asm:9.6'
    }
}


plugins {
    id 'java-library'
    id 'maven-publish'
    id 'idea'
    id 'dev.architectury.loom' version '1.11-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.gradleup.shadow' version '8.3.6' apply false
}

architectury {
    minecraft = project.minecraft_version
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add('--enable-preview')
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version

    repositories {
        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
        maven {
            name = 'Fabric Maven'
            url = 'https://maven.fabricmc.net'
        }
    }
}

import org.objectweb.asm.*
import java.util.jar.*


subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    loom {
        silentMojangMappingsLicense()

        runs {
            client {
                property 'fabric.log.level', project.mod_log_level
                vmArgs '--enable-preview'
            }

            server {
                property 'fabric.log.level', project.mod_log_level
                vmArgs '--enable-preview'
            }
        }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"

        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.parchment_minecraft_version}:${rootProject.parchment_mappings_version}@zip")
        }
    }

    processResources {
        var replaceProperties = [minecraft_version      : minecraft_version,
                                 minecraft_version_range: minecraft_version_range,
                                 neo_version            : neoforge_version,
                                 neo_version_range      : neo_version_range,
                                 loader_version_range   : loader_version_range,
                                 fabric_loader_version  : fabric_loader_version,
                                 mod_id                 : mod_id,
                                 mod_name               : mod_name,
                                 mod_license            : mod_license,
                                 mod_version            : mod_version,
                                 mod_authors            : mod_authors,
                                 mod_description        : mod_description]
        inputs.properties replaceProperties
        filesMatching('META-INF/neoforge.mods.toml') {
            expand replaceProperties
        }
        filesMatching('fabric.mod.json') {
            expand replaceProperties
        }
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        options.release = 21
        options.compilerArgs.add('--enable-preview')
    }
    jar.doLast {
        // 获取生成的 jar 文件路径
        File jarFile = archiveFile.get().asFile
        println "正在移除预览版标记: ${jarFile.name}..."

        // 创建一个临时文件
        File tempFile = new File(jarFile.parent, jarFile.name + ".temp")

        // 这里的逻辑就是把你 Main.java 里的代码翻译成了 Groovy
        try (JarFile jar = new JarFile(jarFile)
             JarOutputStream jos = new JarOutputStream(new FileOutputStream(tempFile))) {

            Enumeration<JarEntry> entries = jar.entries()
            while (entries.hasMoreElements()) {
                JarEntry entry = entries.nextElement()
                InputStream is = jar.getInputStream(entry)

                if (!entry.getName().endsWith(".class")) {
                    jos.putNextEntry(new JarEntry(entry.getName()))
                    is.transferTo(jos)
                    continue
                }

                // ASM 读取和修改
                ClassReader cr = new ClassReader(is)
                ClassWriter cw = new ClassWriter(0)

                // 自定义 Visitor
                ClassVisitor cv = new ClassVisitor(Opcodes.ASM9, cw) {
                    @Override
                    void visit(int version, int access, String name, String signature, String superName, String[] interfaces) {
                        // 关键修复：移除版本号中的预览标记 (0xFFFF0000)
                        int newVersion = version & ~Opcodes.V_PREVIEW
                        // 关键修复：移除 Access Flag 中的预览标记 (虽然 Version 才是导致报错的主因)
                        // 0x10000 在某些版本指 ACC_RECORD，但也可能指预览。
                        // 这里只修改 version 即可解决 UnsupportedClassVersionError，
                        // 但为了和你之前的逻辑一致，如果不影响 Record 功能，可以不改 Access。
                        // 这里我们只改 Version，这是最稳妥的。
                        super.visit(newVersion, access, name, signature, superName, interfaces)
                    }
                }

                cr.accept(cv, 0)

                JarEntry newEntry = new JarEntry(entry.getName())
                jos.putNextEntry(newEntry)
                jos.write(cw.toByteArray())
            }
        }

        // 3. 用修复后的文件覆盖原文件
        if (jarFile.delete()) {
            if (!tempFile.renameTo(jarFile)) {
                throw new RuntimeException("无法重命名临时文件！")
            }
            println "预览版标记移除成功！"
        } else {
            throw new RuntimeException("无法删除原始 Jar 文件！")
        }
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
